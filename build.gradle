plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.8.20'
    id 'pmd'
    id 'name.remal.sonarlint' version '3.0.6'
    id 'org.sonarqube' version '3.3'
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.9.11'
    //id 'io.gitlab.arturbosch.detekt' version '1.22.0' // Disabled because it throws an exception on JDK 19.
    id 'org.jlleitschuh.gradle.ktlint' version '11.2.0'
    id 'io.spring.dependency-management' version '1.0.13.RELEASE'
}

group 'com.nelkinda.training.gameoflife'
version '0.0.1-SNAPSHOT'

sourceCompatibility = 19

repositories {
    mavenCentral()
}

dependencyManagement {
    dependencies {
        dependencySet(group: 'io.cucumber', version: '7.6.0') {
            entry 'cucumber-java'
            entry 'cucumber-junit-platform-engine'
        }
        dependencySet(group: 'net.sourceforge.pmd', version: '6.48.0') {
            entry 'pmd'
            entry 'pmd-core'
            entry 'pmd-java'
        }
        dependency 'org.hamcrest:hamcrest:2.2'
        dependencySet(group: 'org.junit.jupiter', version: '5.9.0') {
            entry 'junit-jupiter'
            entry 'junit-jupiter-api'
            entry 'junit-jupiter-engine'
            entry 'junit-jupiter-params'
        }
        dependencySet(group: 'org.junit.platform', version: '1.9.0') {
            entry 'junit-platform-commons'
            entry 'junit-platform-engine'
            entry 'junit-platform-launcher'
            entry 'junit-platform-suite-api'
            entry 'junit-platform-suite-commons'
            entry 'junit-platform-suite-engine'
        }
        dependency 'org.pitest:pitest-junit5-plugin:1.1.2'
        dependency 'org.pitest:pitest-command-line:1.11.0'
    }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    testImplementation 'io.cucumber:cucumber-java'
    testImplementation 'io.cucumber:cucumber-junit-platform-engine'
    testImplementation 'org.hamcrest:hamcrest'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testImplementation 'org.junit.platform:junit-platform-commons'
    testImplementation 'org.junit.platform:junit-platform-engine'
    testImplementation 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.junit.platform:junit-platform-suite-api'
    testImplementation 'org.junit.platform:junit-platform-suite-commons'
    testImplementation 'org.junit.platform:junit-platform-suite-engine'

    pitest 'org.pitest:pitest-junit5-plugin'
    pitest 'org.pitest:pitest-command-line'
}

jacoco {
    toolVersion = "0.8.8"
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 1.0
            }
        }
    }
}
check.dependsOn jacocoTestCoverageVerification

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = true
        html.required = true
    }
}
test.finalizedBy jacocoTestReport

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict", "-Xjvm-default=enable"]
        jvmTarget = "19"
    }
}

ktlint {
    version = '0.48.2'
    verbose = true
}

pitest {
    targetClasses.set(['com.nelkinda.training.gameoflife.*'])
    pitestVersion.set('1.11.0')
    timestampedReports.set(false)
    outputFormats.set(['XML', 'HTML'])
    mutators.set(['DEFAULTS', 'STRONGER', 'CONSTRUCTOR_CALLS', 'INLINE_CONSTS', 'REMOVE_CONDITIONALS', 'REMOVE_INCREMENTS'])
    mutationThreshold.set(100)
    coverageThreshold.set(100)
}

pmd {
    ruleSets = []
    consoleOutput = true
    dependencies {
        pmd 'net.sourceforge.pmd:pmd'
        pmd 'net.sourceforge.pmd:pmd-core'
        pmd 'net.sourceforge.pmd:pmd-java'
    }
}

pmdMain {
    ruleSetFiles = files("config/pmd/pmdMain.xml")
}

pmdTest {
    ruleSetFiles = files("config/pmd/pmdTest.xml")
}

test {
    useJUnitPlatform()
}

wrapper {
    gradleVersion '8.0'
    distributionType = Wrapper.DistributionType.ALL
}
